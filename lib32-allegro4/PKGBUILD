# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=allegro4
pkgname=lib32-${_pkgbase}
pkgver=4.4.2
pkgrel=2
pkgdesc="Portable library mainly aimed at video game and multimedia programming (legacy version)"
arch=('i686' 'x86_64')
url="http://alleg.sourceforge.net/"
license=('custom')
depends=('lib32-jack' 'lib32-libxpm' 'lib32-libxxf86vm' 'lib32-libxxf86dga' 'lib32-libxcursor' 'lib32-alsa-lib' "${_pkgbase}")
makedepends=('cmake' 'lib32-libpng' 'lib32-mesa' 'gcc-multilib')
source=(http://downloads.sourceforge.net/alleg/allegro-${pkgver}.tar.gz)
md5sums=('4db71b0460fc99926ae91d223199c2e6')

build() {
  rm -rf build
  mkdir build && cd build

  cmake "../allegro-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWANT_DOCS=OFF \
  	-DLIB_SUFFIX="32" \
	-DCMAKE_C_FLAGS="$CFLAGS -m32" \
	-DCMAKE_CXX_FLAGS="$CXXFLAGS -m32"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include}
  install -d -m 755 "${pkgdir}"/usr/share/licenses/
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/${pkgname}
}
