# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libsoup
pkgname=lib32-${_pkgbase}
pkgver=2.42.1
pkgrel=1
pkgdesc="GNOME HTTP Library (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gnome.org"
depends=('lib32-glib2' 'lib32-libxml2' 'lib32-sqlite' "${_pkgbase}")
makedepends=('lib32-libgnome-keyring' 'lib32-glib-networking' 'python2' 'intltool' 'gcc-multilib' 'gobject-introspection' "${_pkgbase}")
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/2.42/${_pkgbase}-${pkgver}.tar.xz)
conflicts=(lib32-libsoup-gnome)
replaces=(lib32-libsoup-gnome)
options=('!libtool' '!emptydirs')
sha256sums=('20bd5365bb3e7de70fade91ac82c9dea31636318df90b8c26bc4cde2523a8c91')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  sed -i -e '1s/python$/&2/' libsoup/tld-parser.py
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 --disable-tls-check
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}
