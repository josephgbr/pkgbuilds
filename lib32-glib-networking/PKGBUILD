# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=glib-networking
pkgname=lib32-${_pkgbase}
pkgver=2.34.2
pkgrel=1
pkgdesc="Network-related giomodules for glib (32 bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('lib32-glib2' 'lib32-libproxy' 'lib32-gnutls' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib' 'ca-certificates')
options=('!libtool')
source=(http://download.gnome.org/sources/${_pkgbase}/2.34/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('00a92ec7b31f03a2fc1eeff568c0f1664e109b3add8c7042d2d420953f49dfde')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib32/glib-networking \
    --disable-static --libdir=/usr/lib32 \
    --disable-nls
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share"
}

