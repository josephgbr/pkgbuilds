# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=glib-networking
pkgname=lib32-$_pkgbase
pkgver=2.36.2
pkgrel=1
pkgdesc="Network-related giomodules for glib (32 bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('lib32-glib2' 'lib32-libproxy' 'lib32-gnutls' "$_pkgbase")
makedepends=('intltool' 'gcc-multilib' 'ca-certificates')
options=('!libtool')
source=(http://download.gnome.org/sources/$_pkgbase/2.36/$_pkgbase-$pkgver.tar.xz)
sha256sums=('2108d55b0af3eea56ce256830bcaf1519d6337e0054ef2eff80f2c0ef0eb23f9')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd $_pkgbase-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib32/$_pkgbase --disable-static \
    --libdir=/usr/lib32 --disable-nls
  make
}

package() {
  make -C $_pkgbase-$pkgver DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share"
}

