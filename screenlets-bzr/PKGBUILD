# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=screenlets-bzr
pkgver=831
pkgrel=1
pkgdesc="Widget framework that consists of small owner-drawn applications (bzr version)"
arch=('any')
url="http://screenlets.org/"
license=('GPL3')
depends=('python2-beautifulsoup3' 'python2-dbus' 'python2-gconf'
         'python2-gnomekeyring' 'python2-rsvg' 'pywebkitgtk' 'python2-wnck'
         'python2-xdg' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('bzr')
optdepends=('screenlets-pack-basic: Desktop widgets'
            'screenlets-pack-full-bzr: Desktop widgets (all of them)')
provides=('screenlets')
conflicts=('screenlets')
install=screenlets-bzr.install

_bzrtrunk=http://bazaar.launchpad.net/~screenlets-dev/screenlets/trunk
_bzrmod=screenlets

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr --no-plugins  pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  # Fix python2
  find . -name \*.py -exec sed -i 's@/usr/bin/python@/usr/bin/python2@' {} \;
  find . -name \*.py -exec sed -i 's@/usr/bin/env python@/usr/bin/python2@' {} \;
  sed -i 's/python -u/python2 -u/' \
        src/bin/* \
        src/lib/*.py \
        src/share/screenlets-manager/*.py
}

package() {
  cd "$srcdir/$_bzrmod-build"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
