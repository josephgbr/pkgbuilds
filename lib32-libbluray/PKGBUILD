# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libbluray
pkgname=lib32-${_pkgbase}
pkgver=0.2.3
pkgrel=2
pkgdesc="Library to access Blu-Ray disks for video playback (32bit)"
arch=('x86_64')
url="http://www.videolan.org/developers/libbluray.html"
license=('LGPL2.1')
depends=('lib32-libxml2')
makedepends=('apache-ant' 'java-environment')
options=(!libtool)
source=(ftp://ftp.videolan.org/pub/videolan/$_pkgbase/$pkgver/$_pkgbase-$pkgver.tar.bz2)
sha512sums=('b84fd172523facb207134bb1fee09c7f65914d22f6120fbfd6f312f0435229f3b110e8cc8ea121159929cda3760cfac4b52f84de39d0fb976f8c88d1f7e6d278')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  cd "$_pkgbase-$pkgver"

  . /etc/profile.d/jdk.sh
  ./configure --prefix=/usr \
    --enable-bdjava \
    --with-jdk=${JAVA_HOME} \
    --disable-static \
    --libdir=/usr/lib32 \
    --disable-doxygen-doc
  make
}

package() {
  cd "$_pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}

# vim:set ts=2 sw=2 et:
