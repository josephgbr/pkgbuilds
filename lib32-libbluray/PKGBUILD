# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libbluray
pkgname=lib32-${_pkgbase}
pkgver=0.3.0
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback (32bit)"
arch=('x86_64')
url="http://www.videolan.org/developers/libbluray.html"
license=('LGPL2.1')
depends=('lib32-libxml2')
makedepends=('gcc-multilib')
options=(!libtool)
source=(ftp://ftp.videolan.org/pub/videolan/$_pkgbase/$pkgver/$_pkgbase-$pkgver.tar.bz2)
sha512sums=('96eedf23ff756e97fa64d9624a49608daccbac169714b9b231a284ed4eb63c77ee2ecbdfc42c311781571146cfa0fe340854ac831d3fe1312131e1742a4a7b02')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  cd $_pkgbase-$pkgver
  
  ./configure --prefix=/usr \
    --disable-static \
    --libdir=/usr/lib32 \
    --disable-doxygen-doc
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim:set ts=2 sw=2 et:
