# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbase=libnotify
pkgname=lib32-${_pkgbase}
pkgver=0.7.5
pkgrel=2
pkgdesc="Desktop notification library (32-bit)"
arch=('x86_64')
url="http://library.gnome.org/devel/notification-spec/"
license=('LGPL')
depends=('lib32-gdk-pixbuf2' "${_pkgbase}")
makedepends=('lib32-gtk3' 'gcc-multilib' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbase}/0.7/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('4378cb47874ba4d5f759b7890d8d28d086e81d3f0bd4f6e84e5ab66d82ffb652')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
