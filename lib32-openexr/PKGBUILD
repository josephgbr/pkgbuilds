# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=openexr
pkgname=lib32-$_pkgbase
pkgver=1.7.1
pkgrel=1
pkgdesc="Openexr library for EXR images (32 bit)"
arch=('x86_64')
url="http://www.openexr.org"
license=('BSD' 'custom:OpenEXR')
depends=('lib32-zlib' 'lib32-ilmbase' "$_pkgbase")
makedepends=('gcc-multilib')
options=('!libtool')
source=("https://github.com/downloads/${_pkgbase}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        'gcc43.patch')
md5sums=('a9e0369dfd68941c50cdaa93e9d7a8d1'
         'd91fae759053a70bcedcf02eb33ca4a2')

build() {
  export CXX='g++ -m32'
  cd ${_pkgbase}-${pkgver}
  patch -p0 -i ${srcdir}/gcc43.patch
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname} "${pkgdir}"/usr/share/licenses/${_pkgbase}
}
