# Contributor: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer : josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libproxy
pkgname=lib32-${_pkgbase}
pkgver=0.4.11
pkgrel=1
pkgdesc="A library that provides automatic proxy configuration management (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-gcc-libs' "${_pkgbase}")
makedepends=('gcc-multilib' 'cmake')
url="http://libproxy.googlecode.com"
source=(http://libproxy.googlecode.com/files/${_pkgbase}-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('3cd1ae2a4abecf44b3f24d6639d2cd84')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # Disable gnome manually
  sed -i '/option(WITH_GNOME3/s/ON/OFF/' ${_pkgbase}-${pkgver}/libproxy/cmake/modules/config_gnome.cmk

  mkdir build || true
  cd build
  cmake ../${_pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_SUFFIX=32 \
    -DLIBEXEC_INSTALL_DIR=/usr/lib32/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=no \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DWITH_WEBKIT=OFF \
    -DWITH_KDE4=no \
    -DWITH_MOZJS=no \
    -DWITH_NM=no \
    -DWITH_PERL=no \
    -DWITH_PYTHON=no
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
