# Contributor: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer : josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libproxy
pkgname=lib32-${_pkgbase}
pkgver=0.4.7
pkgrel=2
pkgdesc="A library that provides automatic proxy configuration management (32-bit)"
arch=('x86_64')
url="http://libproxy.googlecode.com"
license=('LGPL')
depends=('lib32-gcc-libs' "${_pkgbase}")
makedepends=('gcc-multilib' 'cmake')
options=('!libtool')
source=("http://libproxy.googlecode.com/files/${_pkgbase}-${pkgver}.tar.gz"
        'libproxy-0.4.7-unistd.patch')
md5sums=('509e03a488a61cd62bfbaf3ab6a2a7a5'
         'b05efe2c4156bf7f469125d58e626a19')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # Fix build with GCC 4.7 (patch from Fedora)
  patch -d ${_pkgbase}-${pkgver} -Np1 -i "${srcdir}/libproxy-0.4.7-unistd.patch"

  mkdir build || true
  cd build
  cmake ../${_pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_SUFFIX=32 \
    -DLIBEXEC_INSTALL_DIR=/usr/lib32/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=no \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DWITH_KDE4=no \
    -DWITH_MOZJS=no \
    -DWITH_NM=no \
    -DWITH_PERL=no \
    -DWITH_PYTHON=no \
    -DWITH_WEBKIT=no
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
