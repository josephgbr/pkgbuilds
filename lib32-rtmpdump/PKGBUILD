# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=rtmpdump
pkgname=lib32-${_pkgbase}
pkgver=20121230
pkgrel=1
pkgdesc="A tool to download rtmp streams (32 bit)"
arch=('x86_64')
url="http://rtmpdump.mplayerhq.hu/"
license=('GPL2' 'LGPL2.1')
depends=('lib32-openssl' "${_pkgbase}")
makedepends=('gcc-multilib')
options=(!makeflags)
#git://git.ffmpeg.org/rtmpdump
source=(ftp://ftp.archlinux.org/other/${_pkgbase}/${_pkgbase}-git-${pkgver}.tar.xz)
sha256sums=('2612ccbc041281c90d1fa59d20aec783fa3ef29de952323d8d89699fc3d5c4d2')

build() {
  cd ${_pkgbase}-${pkgver}
  sed -i -e 's:gcc:gcc -m32:' Makefile librtmp/Makefile
  make 
}

package() {
  cd ${_pkgbase}-${pkgver}
  install -dm755 "${pkgdir}/usr/lib32"
  make prefix=/usr mandir=/usr/share/man libdir=/usr/lib32 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,sbin,share}
}
