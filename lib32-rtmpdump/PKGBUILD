# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=rtmpdump
pkgname=lib32-${_pkgbase}
pkgver=20121203
pkgrel=1
pkgdesc="A tool to download rtmp streams (32 bit)"
arch=('x86_64')
url="http://rtmpdump.mplayerhq.hu/"
license=('GPL2' 'LGPL2.1')
depends=('lib32-openssl' "${_pkgbase}")
makedepends=('gcc-multilib')
options=(!makeflags)
#git://git.ffmpeg.org/rtmpdump
source=(ftp://ftp.archlinux.org/other/${_pkgbase}/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('e5d9f7411bddd5a12be677f2a285c38100313db2b3b1f8d2a709d46e03741409')

build() {
  cd ${_pkgbase}-${pkgver}
  
  # CC     = gcc -> gcc -m32
  sed -i -e '/^CC\=/s/gcc/gcc -m32/' Makefile librtmp/Makefile
  	  
  make 
}

package() {
  cd ${_pkgbase}-${pkgver}
  install -dm755 "${pkgdir}/usr/lib32"
  make prefix=/usr mandir=/usr/share/man libdir=/usr/lib32 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,sbin,share}
}
