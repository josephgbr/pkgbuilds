# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libofa
pkgname=lib32-${_pkgbase}
pkgver=0.9.3
pkgrel=2
pkgdesc="An open-source audio fingerprint by MusicIP (32 bit)"
arch=('x86_64')
url="http://code.google.com/p/musicip-libofa/"
license=('GPL2' 'custom')
depends=('lib32-expat' 'lib32-fftw' 'lib32-gcc-libs' "${_pkgbase}")
makedepends=('gcc-multilib' 'lib32-curl')
options=('!libtool')
source=("http://musicip-libofa.googlecode.com/files/${_pkgbase}-${pkgver}.tar.gz"
	    'gcc-4.patch'
	    'gcc4.3.patch'
	    'gcc4.5.patch'
	    'fix_ftbfs.diff'
	    'gcc-47.patch')
sha1sums=('3dec8e1dcea937f74b4165e9ffd4d4f355e4594a'
          'cb19377d0634c03d2a49cfc61915cec918c341f7'
          'cae08bdc12de923d5e032696702a0530ae6eafc1'
          '754d69d66ab46219035ccafeeb3ff62433cca4f2'
          'dfddfb6e9db037f8edce116911855c6732320dca'
          '1ee98744b97128f9b93cecd7b4f1c034a0e713cc')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  
  patch -p0 -i ${srcdir}/gcc-4.patch
  patch -p1 -i ${srcdir}/gcc4.3.patch
  patch -p1 -i ${srcdir}/gcc4.5.patch
  patch -p1 -i ${srcdir}/gcc-47.patch
  #http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=637896
  patch -p1 -i ${srcdir}/fix_ftbfs.diff
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}/usr/include"
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -sf ${_pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}
