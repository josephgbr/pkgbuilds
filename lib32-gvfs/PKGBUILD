# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gvfs
pkgname=lib32-${_pkgbase}
pkgver=1.16.1
pkgrel=1
pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-avahi' 'lib32-fuse' 'lib32-libarchive' 'lib32-libsoup'
         'lib32-udisks2' 'lib32-libsecret' 'lib32-libbluray' 'lib32-gtk3'
         'lib32-libcdio-paranoia' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib'  'lib32-systemd')
url="http://www.gnome.org"
options=(!libtool)
install=gvfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/1.16/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('e7ec47547ff8029a2e8d81533bdfa870fe4a547c9d7c4e1e946d42d38970b0ac')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure \
      --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-static --libdir=/usr/lib32 --libexecdir=/usr/lib32/gvfs \
      --disable-bash-completion --disable-documentation \
      --disable-{nls,hal,gdu,goa,samba,afp,afc,libmtp}
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  sed -e 's/^am__append_4/#am__append_4/' \
      -e 's/^am__append_5/#am__append_5/' \
      -e 's/^am__append_6/#am__append_6/' \
      -e 's/^am__append_7/#am__append_7/' \
      -i monitor/Makefile
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/lib32/gvfs/gvfsd-{gphoto2,obexftp}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
}

