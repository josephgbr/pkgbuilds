# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=slang
pkgname=lib32-${_pkgbase}
pkgver=2.2.4
pkgrel=1
pkgdesc="S-Lang is a powerful interpreted language (32 bit)"
arch=('x86_64')
url="http://www.jedsoft.org/slang/"
license=('GPL')
depends=('lib32-glibc' 'lib32-pcre' 'lib32-zlib' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!makeflags')
source=(ftp://ftp.fu-berlin.de/pub/unix/misc/slang/v${pkgver%.*}/${_pkgbase}-${pkgver}.tar.bz2)
md5sums=('7fcfd447e378f07dd0c0bae671fe6487')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32 --without-png
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install-all
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}
