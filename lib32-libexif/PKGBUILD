# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libexif
pkgname=lib32-${_pkgbase}
pkgver=0.6.21
pkgrel=1
pkgdesc="A library to parse an EXIF file and read the data from those tags (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://sourceforge.net/projects/libexif"
depends=('lib32-glibc' "$_pkgbase")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sf.net/sourceforge/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2)
md5sums=('27339b89850f28c8f1c237f233e05b27')

build() {
  export CC='gcc -m32'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/usr/{include,share,lib32/*.a}

  install -dm755 "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/${pkgname}
}
