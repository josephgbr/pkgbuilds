# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libsoup
pkgname=lib32-${_pkgbase}-gnome
pkgver=2.40.3
pkgrel=1
pkgdesc="GNOME HTTP Library - GNOME libraries (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gnome.org"
depends=('lib32-libgnome-keyring' 'lib32-sqlite' "lib32-${_pkgbase}" "${_pkgbase}-gnome")
makedepends=('lib32-libxml2' 'lib32-glib-networking' 'python2' 'intltool' 'gcc-multilib' 'gobject-introspection' "${_pkgbase}")
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/2.40/${_pkgbase}-${pkgver}.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('82c92f1f6f4cbfd501df783ed87e7de9410b4a12a3bb0b19c64722e185d2bbc9')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  sed -i -e 's/env python/python2/' libsoup/tld-parser.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 --disable-tls-check
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib32/libsoup-2.4.* \
        "${pkgdir}"/usr/lib32/pkgconfig/libsoup-2.4.pc \
        "${pkgdir}"/usr/lib32/girepository-1.0/Soup-2.4.typelib
  rm -rf "${pkgdir}"/usr/include \
         "${pkgdir}"/usr/share
}
