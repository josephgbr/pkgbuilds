# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=fftw
pkgname=lib32-${_pkgbase}
pkgver=3.3.3
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT) (32 bit)"
arch=('x86_64')
license=('GPL2')
url="http://www.fftw.org/"
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-fortran-multilib')
options=('!libtool')
source=("http://www.fftw.org/${_pkgbase}-${pkgver}.tar.gz")
sha1sums=('11487180928d05746d431ebe7a176b52fe205cf9')

# notes:
# http://www.fftw.org/fftw2_doc/fftw_6.html#SEC69
# http://www.fftw.org/faq/section2.html#singleprec
# http://www.fftw.org/fftw3_doc/Precision.html#Precision


build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cp -af ${_pkgbase}-${pkgver} ${_pkgbase}-${pkgver}-double
  cp -af ${_pkgbase}-${pkgver} ${_pkgbase}-${pkgver}-long-double
  mv -f ${_pkgbase}-${pkgver} ${_pkgbase}-${pkgver}-single

  # use upstream default CFLAGS while keeping our -march/-mtune
  CFLAGS+=" -O3 -fomit-frame-pointer -malign-double -fstrict-aliasing -ffast-math"

  CONFIGURE="./configure F77=gfortran --prefix=/usr \
                 --enable-shared --enable-threads \
                 --libdir=/usr/lib32"

  msg "build double precision"
  cd "${srcdir}"/${_pkgbase}-${pkgver}-double
  $CONFIGURE --enable-sse2
  make

  msg "build long double precision"
  cd "${srcdir}"/${_pkgbase}-${pkgver}-long-double
  $CONFIGURE --enable-long-double
  make
 
  msg "build single precision"
  cd "${srcdir}"/${_pkgbase}-${pkgver}-single
  $CONFIGURE --enable-float --enable-sse
  make
}

package() {
  cd "${srcdir}"/${_pkgbase}-${pkgver}-double
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/${_pkgbase}-${pkgver}-long-double
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/${_pkgbase}-${pkgver}-single
  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
