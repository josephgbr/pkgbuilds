# Maintainer: jospehgbr <rafael.f.f1@gmail.com>

_pkgbase=fribidi
pkgname=lib32-${_pkgbase}
pkgver=0.19.5
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://fribidi.org"
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://fribidi.org/download/${_pkgbase}-${pkgver}.tar.bz2")
md5sums=('925bafb97afee8a2fc2d0470c072a155')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
