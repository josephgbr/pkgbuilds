# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgusb
pkgname=lib32-${_pkgbase}
pkgver=0.1.4
pkgrel=1
pkgdesc="GLib wrapper around libusb1 (32 bit)"
arch=('x86_64')
url="https://gitorious.org/gusb/"
license=('LGPL2.1')
depends=('lib32-systemd' 'lib32-libusbx' "${_pkgbase}")
makedepends=('gcc-multilib' 'gobject-introspection')
source=(http://people.freedesktop.org/~hughsient/releases/${_pkgbase}-${pkgver}.tar.xz)
options=('!libtool')
sha256sums=('64294a649e54df2009545f47577f41438ec50c2fc571dd7b08cf32cb14de37b9')

build() {
  export CC='gcc -m32'
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}
