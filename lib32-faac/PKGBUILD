# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=faac
pkgname=lib32-$_pkgbase
pkgver=1.28
pkgrel=5
pkgdesc="FAAC is an AAC audio encoder (32 bit)"
arch=('x86_64')
url="http://www.audiocoding.com/"
license=('GPL' 'custom:FAAC')
depends=('lib32-libmp4v2' "$_pkgbase")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        'altivec.patch' 'mp4v2-1.9.patch' 'mp4v2-2.0.0.patch')
md5sums=('80763728d392c7d789cde25614c878f6'
         'db6b43868ad91127c6a7b57f5c6ca6bb'
         '9d52a17e68fa15ca94e88e1457b94b74'
         '482f515920716899ecda925613f0c31e')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  
  patch -p1 -i ../mp4v2-1.9.patch
  patch -p0 -i ../mp4v2-2.0.0.patch
  patch -p0 -i ../altivec.patch
  find . -type f -print0 | xargs -0 sed -i 's/\r//g'
  ./bootstrap
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32/${_pkgbase}
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  
  make DESTDIR="${pkgdir}" install  
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -Dm644 libfaac/kiss_fft/COPYING \
                   "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
