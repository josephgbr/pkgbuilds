# Maintainer : josephgbr <rafael.f.f1@gmail.com>
# Contributor: TryA <tryagainprod {at} gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbase=libvdpau
pkgname=lib32-libvdpau
pkgver=0.6
pkgrel=1
pkgdesc="Nvidia VDPAU library (32-bit)"
arch=('x86_64')
url=http://cgit.freedesktop.org/~aplattner/libvdpau
license=('custom')
depends=('lib32-gcc-libs' "${_pkgbase}") #libx11
makedepends=('gcc-multilib' 'lib32-libx11' )
options=('!libtool')
source=(http://people.freedesktop.org/~aplattner/vdpau/${_pkgbase}-${pkgver}.tar.gz
        disable-xext.patch)
sha256sums=('9b55beec9d8b01bbf89229b7d075ceee870f4e1830523c11f29a46c07fe72d75'
            '3a78c0978fd3abd42b95e84667d775ee2c7a1bf58f6f9b0e394f0805b09b9ae5')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_pkgbase}-${pkgver}"
  patch -p1 -i "${srcdir}"/disable-xext.patch
  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s $_pkgbase "$pkgdir"/usr/share/licenses/$pkgname
}
