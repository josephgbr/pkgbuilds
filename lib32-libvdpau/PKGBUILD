# Maintainer : josephgbr <rafael.f.f1@gmail.com>
# Contributor: TryA <tryagainprod {at} gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbase=libvdpau
pkgname=lib32-libvdpau
pkgver=0.7
pkgrel=1
pkgdesc="Nvidia VDPAU library (32-bit)"
arch=('x86_64')
url=http://cgit.freedesktop.org/~aplattner/libvdpau
license=('custom')
depends=('lib32-gcc-libs' 'lib32-libxext' "${_pkgbase}") #libx11
makedepends=('gcc-multilib' 'dri2proto' )
options=('!libtool')
source=(http://people.freedesktop.org/~aplattner/vdpau/${_pkgbase}-${pkgver}.tar.gz)
sha256sums=('24dc08467ce8c59d6cfbf0d34d2dd1f86b4ff62e90777e0a8f513c5c9de9bce0')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s $_pkgbase "$pkgdir"/usr/share/licenses/$pkgname
}
