# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libusb-compat
pkgname=lib32-${_pkgbase}
pkgver=0.1.4
pkgrel=3
pkgdesc="Library to enable user space application programs to communicate with USB devices (32 bit)"
arch=('x86_64')
depends=('lib32-libusb' "${_pkgbase}")
makedepends=('gcc-multilib')
url="http://libusb.sourceforge.net/"
license=('LGPL')
options=('!libtool')
source=("http://downloads.sourceforge.net/libusb/${_pkgbase}-0.1/${_pkgbase}-$pkgver/${_pkgbase}-${pkgver}.tar.bz2")
md5sums=('2ca521fffadd0c28fdf174e6ec73865b')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include}
}
