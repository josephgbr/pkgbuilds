# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=gnome-vfs
pkgname=lib32-$_pkgbase
pkgver=2.24.4
pkgrel=8
pkgdesc="The GNOME Virtual File System (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-bzip2' 'lib32-avahi' 'lib32-smbclient' 'lib32-gconf'
         'lib32-krb5' 'lib32-gnutls' 'lib32-libgcrypt' "$_pkgbase")
makedepends=('intltool' 'gtk-doc' 'gnome-common' 'lib32-dbus-glib'
             'gnome-mime-data' 'lib32-keyutils')
options=('!libtool' '!emptydirs' '!docs')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbase/2.24/$_pkgbase-${pkgver}.tar.bz2
        gnutls-config.patch
        gcrypt-config.patch
        fix-automake.patch
        patch-port-glib2-2.6.31-and-up.diff)
sha256sums=('62de64b5b804eb04104ff98fcd6a8b7276d510a49fbd9c0feb568f8996444faa'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d'
            'c059e218f310da683778919d36e7862f7e763384805f6453d328fbaf507a8114'
            'a4ca3d2fc723e755535cff73b5cb7e2a780ee0b478ca34163a164eac7f3e264a'
            'e61f71e36c7c1d2e91935e8f72726df24a59cd70a972b1ed96f7f54a62658e11')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32' 
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
        
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  
  #Fix build with new gnutls
  patch -Np1 -i "${srcdir}/gnutls-config.patch"
  #fix build with new libgcrypt >= 1.5.0
  patch -Np1 -i "${srcdir}/gcrypt-config.patch"
  #fix automake 1.13
  patch -Np1 -i "${srcdir}/fix-automake.patch"
  # fix deprecated functions https://trac.macports.org/ticket/38657
  patch -Np0 -i "${srcdir}/patch-port-glib2-2.6.31-and-up.diff"

  libtoolize --force
  gtkdocize
  aclocal
  autoreconf -i
  automake
  
  ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
        --libexecdir=/usr/lib32/gnome-vfs-2.0 \
        --enable-{avahi,gnutls,samba} \
        --disable-{hal,howl,openssl} \
        --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/{etc,usr/{bin,include,share}}
}
