# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgdiplus
pkgname=lib32-$_pkgbase
pkgver=2.10
pkgrel=2
pkgdesc="An Open Source Implementation of the GDI+ API (32 bit)"
arch=('x86_64')
license=('MPL' 'LGPL')
url="http://www.mono-project.com"
depends=('lib32-libtiff' 'lib32-cairo' 'lib32-libexif' 'lib32-giflib' 'lib32-glib2' "$_pkgbase")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://download.mono-project.com/sources/$_pkgbase/$_pkgbase-$pkgver.tar.bz2
        libpng15.patch)
md5sums=('451966e8f637e3a1f02d1d30f900255d'
         '33991a54fba0eda645b745ce290c79b9')
         
build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p0 -i ../libpng15.patch
  ./configure --prefix=/usr --with-cairo=system \
                --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
