# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=neon
pkgname=lib32-${_pkgbase}
pkgver=0.29.6
pkgrel=1
pkgdesc="HTTP and WebDAV client library with a C interface (32 bit)"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('lib32-krb5' 'lib32-expat' "${_pkgbase}") #'ca-certificates'
makedepends=('gcc-multilib')
url="http://www.webdav.org/neon/"
source=("http://www.webdav.org/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('591e0c82e6979e7e615211b386b8f6bc')
options=('libtool') # FS#16067

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}"/${_pkgbase}-${pkgver}
  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --libdir=/usr/lib32 --without-libproxy # without proxy, just like 'neon' package
  make

  # fix invalid .so links in man pages (FS#24902)
  sed -i '/^\.so/s|\.so \([^.]\+\)\.\([[:digit:]]\)|.so man\2/\1.\2|' doc/man/*
}

package() {
  cd "${srcdir}"/${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
