# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgphoto2
pkgname=lib32-${_pkgbase}
pkgver=2.5.0
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs. (32 bit)"
arch=('x86_64')
url="http://www.gphoto.org"
license=(LGPL)
depends=('lib32-libexif' 'lib32-libjpeg-turbo'  'lib32-libltdl' 'lib32-libusb-compat' "${_pkgbase}") #'lib32-gd' 'lib32-libusbx'
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sourceforge.net/gphoto/${_pkgbase}-${pkgver}.tar.gz)
md5sums=('ea9b3b07a3738c0a77958d6fe13ae524')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --disable-rpath --libdir=/usr/lib32 --without-hal
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share,lib32/udev}
}
