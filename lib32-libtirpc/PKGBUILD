# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Franco Tortoriello <franco.tortoriello@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_pkgbase=libtirpc
pkgname=lib32-${_pkgbase}
pkgver=0.2.3
pkgrel=1
pkgdesc="Transport Independent RPC library (SunRPC replacement) (lib32)"
arch=('x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('lib32-libgssglue' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2
        libtirpc-build.patch)
md5sums=('b70e6c12a369a91e69fcc3b9feb23d61'
         'd4c0eef2f0891abf06cd4146eb51ed36')

build() {
  cd $_pkgbase-$pkgver
  patch -p1 -i "$srcdir"/libtirpc-build.patch
  ./configure --prefix=/usr --sysconf=/etc --enable-gss \
        --libdir=/usr/lib32 CC="gcc -m32"
  make
}

package() {
  make -C $_pkgbase-$pkgver DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{include,share}}
  install -dm755 "${pkgdir}"/usr/share/licenses/
  ln -s $_pkgbase "${pkgdir}"/usr/share/licenses/$pkgname
}
