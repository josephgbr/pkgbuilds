# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libusb
pkgname=lib32-${_pkgbase}
pkgver=1.0.9
pkgrel=1
pkgdesc="Library to enable user space application programs to communicate with USB devices. (32-bit)"
arch=('x86_64')
url="http://libusb.sourceforge.net/"
license=('LGPL')
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-multilib')
source=(http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2)
options=(!libtool)
md5sums=('7f5a02375ad960d4e33a6dae7d63cfcb')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "libusb-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package () {
  cd "${srcdir}/libusb-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}

