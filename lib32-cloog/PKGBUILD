# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=cloog
pkgname=lib32-${_pkgbase}
pkgver=0.17.0
pkgrel=2
pkgdesc="Library that generates loops for scanning polyhedra (32 bit)"
arch=('x86_64')
url="http://www.bastoul.net/cloog/"
license=('GPL')
depends=('lib32-isl' 'lib32-gmp' "${_pkgbase}")
conflicts=('lib32-cloog-ppl')
options=('!libtool')
source=("${url}/pages/download/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('0aa3302c81f65ca62c114e5264f8a802')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --with-isl=system --with-gmp=system --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbase}-${pkgver}
  make check
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}/usr"/{bin,include}
}
