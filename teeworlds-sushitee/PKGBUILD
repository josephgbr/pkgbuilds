# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teeworlds-sushitee
pkgver=0.6.2
pkgrel=1
pkgdesc="SushiTee customized version of this 2D shooting game"
arch=('i686' 'x86_64')
url="https://www.teeworlds.com"
license=('custom')
depends=('alsa-lib' 'glu' 'sdl' 'freetype2')
makedepends=('python' 'bam' 'mesa')
provides=('teeworlds')
conflicts=('teeworlds')
source=(https://github.com/SushiTee/teeworlds/archive/Client-Pack-0.6.tar.gz
        teeworlds.desktop
		teeworlds.png)
md5sums=('f328e401de98bb0f2a9d7ded1a0398bd'
         '3944aaf7a8c19a83510b2b39c4cddf5f'
         '44f39e398409232a0e0a431ed46d8cc8')

build() {
	# Build teeworlds
	cd teeworlds-Client-Pack-0.6
	bam server_release client_release
}

package() {
	cd teeworlds-Client-Pack-0.6

	# Install data files
	mkdir -p "${pkgdir}"/usr/share/teeworlds/data
	cp -r data/* "${pkgdir}"/usr/share/teeworlds/data

	install -Dm755 teeworlds "${pkgdir}"/usr/bin/teeworlds
	install -Dm755 teeworlds_srv "${pkgdir}"/usr/bin/teeworlds_srv

	install -Dm644 "${srcdir}"/teeworlds.desktop \
		"${pkgdir}"/usr/share/applications/teeworlds.desktop
	install -Dm644 "${srcdir}"/teeworlds.png \
		"${pkgdir}"/usr/share/pixmaps/teeworlds.png

	install -Dm644 license.txt \
		"${pkgdir}"/usr/share/licenses/teeworlds/license.txt
}
