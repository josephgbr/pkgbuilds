# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_name=d-feet
pkgname=${_name}-git
pkgver=0.3.4.14.g4d4a82b
pkgrel=1
pkgdesc="D-Bus debugger for GNOME"
arch=(i686 x86_64)
url="https://live.gnome.org/DFeet"
license=(GPL)
depends=('gtk3' 'python-gobject' 'hicolor-icon-theme')
makedepends=('python-distribute' 'intltool' 'itstool' 'pep8-python3'
             'git' 'gnome-common')
options=(!emptydirs)
provides=($_name)
conflicts=($_name)
install=$pkgname.install
source=("git://git.gnome.org/$_name")
sha256sums=('SKIP')

pkgver() {
  cd $_name
  git describe | sed 's/^v//;s/-/./g;s/_/./g'
}

build() {
  cd $_name
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $_name
  make DESTDIR="$pkgdir" install
}
