# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teighaviewer
pkgver=3.8.1
_ver=3.08.1
pkgrel=2
pkgdesc="Free cross-platform application for the visualization of CAD drawing files."
arch=('i686' 'x86_64')
url="http://www.opendesign.com/guestfiles/teigha_viewer"
license=('custom')

if [ $CARCH == i686 ]; then
  depends=('qt4' 'mesa' 'bash' 'hicolor-icon-theme' 'glu')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-qt4' 'lib32-mesa' 'bash' 'hicolor-icon-theme' 'lib32-glu')
fi

install=${pkgname}.install
source=(http://www.opendesign.com/files/guestdownloads/TeighaViewer/TeighaViewer-${pkgver}.i386.deb)
md5sums=('1bde6388ffe976698ba3aedaf66eaa9f')

build() {
  [ ! -d ${pkgname}-${pkgver} ] && mkdir ${pkgname}-${pkgver}
  
  ar xo TeighaViewer-${pkgver}.i386.deb data.tar.gz
  tar xf data.tar.gz -C ${pkgname}-${pkgver}
}

package() {
  cd ${pkgname}-${pkgver}
  
  # persistent application storage (libs, executables, etc.)
  if [ $CARCH == i686 ]; then
    appstoragedir=usr/lib/teighaviewer
  elif [ $CARCH == x86_64 ]; then
    appstoragedir=usr/lib32/teighaviewer
  fi
  for file in $(ls usr/bin/TeighaViewer_${_ver}/); do
    install -Dm755 usr/bin/TeighaViewer_${_ver}/${file} \
        "${pkgdir}/${appstoragedir}/${file}"
  done
  
  # binary
  install -Dm755 usr/bin/TeighaViewer \
    "${pkgdir}/usr/bin/teighaviewer"
  sed -e "s#usr/bin/TeighaViewer_${_ver}#${appstoragedir}#" \
    -i "${pkgdir}/usr/bin/${pkgname}"
  
  # desktop item
  install -Dm644 usr/share/applications/TeighaViewer_${_ver}.desktop \
    "${pkgdir}/usr/share/applications/teighaviewer.desktop"
  sed -e "s#/TeighaViewer#/teighaviewer#" \
    -i "${pkgdir}/usr/share/applications/teighaviewer.desktop"
  
  # hicolor icons
  for icon in $(find usr/share/icons -type f); do
    install -Dm644 ${icon} \
        "${pkgdir}/${icon}"
  done
  
  # copyright
  install -Dm644 usr/share/doc/TeighaViewer/copyright \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
  
}
