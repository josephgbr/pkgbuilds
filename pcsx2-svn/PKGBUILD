# Maintainer: josephgbr <rafael.f.f1 at gmail.com>
# Contributor: Themaister <maister at archlinux.us>

# Hint: for a cleaner but less informative usage, replace "Debug"
# with "Release" in the line '-DCMAKE_BUILD_TYPE=' below

pkgname=pcsx2-svn
pkgver=5388
pkgrel=1
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == i686 ]; then
    depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew' 'portaudio'
             'libgl' 'alsa-lib' 'libjpeg-turbo' 'soundtouch'
             'gtk-engines' 'sdl')
    makedepends=('cmake' 'subversion' 'sparsehash')
elif [ $CARCH == x86_64 ]; then
    depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
             'lib32-libgl' 'lib32-glew' 'lib32-libjpeg-turbo'
             'lib32-portaudio' 'lib32-alsa-lib' 'lib32-soundtouch'
             'lib32-gtk-engines' 'lib32-sdl')
    makedepends=('cmake' 'subversion' 'sparsehash' 'gcc-multilib')
fi
provides=('pcsx2')
conflicts=('pcsx2' 'pcsx2-bin')
options=('!emptydirs' '!strip') # if build type = debug, then avoid strip symbols
install="${pkgname}.install"
changelog="ChangeLog"

_svntrunk=http://pcsx2.googlecode.com/svn/trunk
_svnmod=pcsx2

build() {
  cd "${srcdir}"
  msg "Connecting to SVN server...."
  if [[ -d "${_svnmod}/.svn" ]]; then
    (cd "${_svnmod}" && svn up -r "${pkgver}")
  else
    svn co "${_svntrunk}" --config-dir ./ -r "${pkgver}" "${_svnmod}"
  fi
  rm -rf "${srcdir}/${_svnmod}-build"
  msg "SVN checkout done or server timeout"
  
  # For ArchLinux 64-bit, install plugins in lib32 folder
  _plugindir=/usr/lib/pcsx2
  [ $CARCH == x86_64 ] && _plugindir=/usr/lib32/pcsx2
  
  svn export --quiet "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"
  
  # Disable plugins as they are only for debug performance and might confuse users
  mv plugins/GSnull plugins/GSnull-disabled
  mv plugins/SPU2null plugins/SPU2null-disabled
  
  msg "Starting cmake and build..."
  cmake CMakeLists.txt \
      -DCMAKE_BUILD_TYPE="Debug" \
      -DPACKAGE_MODE=TRUE \
      -DCMAKE_INSTALL_PREFIX="/usr" \
      -DPLUGIN_DIR=${_plugindir} \
      -DGAMEINDEX_DIR="/usr/share/pcsx2" \
      -DGLSL_SHADER_DIR="/usr/share/pcsx2" \
      -DXDG_STD=TRUE
  make
}

package() {
  cd "${_svnmod}-build"
  make DESTDIR="${pkgdir}" install
}
