# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=cracklib
pkgname=lib32-$_pkgbase
pkgver=2.9.0
pkgrel=1
pkgdesc="Password Checking Library (32 bit)"
arch=('x86_64')
url="http://sourceforge.net/projects/cracklib"
license=('LGPL')
depends=('lib32-glibc' 'lib32-zlib' "$_pkgbase")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$_pkgbase/$_pkgbase-$pkgver.tar.gz)
md5sums=('e0f94ac2138fd33c7e77b19c1e9a9390')

build() {
  cd $_pkgbase-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin \
      --libdir=/usr/lib32 --without-python CC='gcc -m32'
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/usr"/{bin,include,sbin,share}
}
