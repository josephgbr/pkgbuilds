# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libsecret
pkgname=lib32-${_pkgbase}
pkgver=0.12
pkgrel=1
pkgdesc='library for storing and retrieving passwords and other secrets. (32 bit)'
arch=('x86_64')
license=('LGPL')
url="https://live.gnome.org/Libsecret"
depends=('lib32-glib2' 'lib32-libgcrypt' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${pkgver}/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('323db75a5e73058d3eb95597d9f798fd715ec7d58f500be84628b8f9a0617c01')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 --disable-manpages \
      --disable-vala
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
