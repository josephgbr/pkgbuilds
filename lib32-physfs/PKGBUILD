# Maintainer: josephbr <rafael.f.f1@gmail.com>

_pkgbase=physfs
pkgname=lib32-$_pkgbase
pkgver=2.0.3
pkgrel=2
pkgdesc="A library to provide abstract access to various archives (32 bit)"
arch=('x86_64')
url="http://icculus.org/physfs/"
license=('ZLIB')
depends=('lib32-zlib' "${_pkgbase}")
makedepends=('cmake' 'doxygen' 'gcc-multilib')
source=("http://icculus.org/physfs/downloads/${_pkgbase}-${pkgver}.tar.bz2")
sha1sums=('327308c777009a41bbabb9159b18c4c0ac069537')

build() {
  cd ${_pkgbase}-${pkgver}
  
  rm -rf build
  mkdir build && cd build 
  export CFLAGS="$CFLAGS -m32 -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -m32 -fno-strict-aliasing"
  
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHYSFS_BUILD_TEST=OFF \
    -DPHYSFS_BUILD_WX_TEST=OFF \
    -DLIB_SUFFIX=32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include}
  
  install -dm755 "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/${pkgname}
}
