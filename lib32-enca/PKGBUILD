# Maintainer: jospehgbr <rafael.f.f1@gmail.com>

_pkgbase=enca
pkgname=lib32-${_pkgbase}
pkgver=1.13
pkgrel=1
pkgdesc="Charset analyser and converter (32 bit)"
arch=('x86_64')
url="http://freshmeat.net/projects/enca/"
options=('!libtool')
license=('GPL')
depends=('recode' "${_pkgbase}")
makedepends=('gcc-multilib' 'bash')
source=("http://dl.cihar.com/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2")
md5sums=('762bead5d9a0a5e55024e1102baaa89d')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib32 \
              --mandir=/usr/share/man \
              --with-librecode=/usr \
              --enable-external \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
