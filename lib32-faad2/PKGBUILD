# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>
# Contributor: escoand <passtschu at freenet.de>

_pkgbase=faad2
pkgname=lib32-${_pkgbase}
pkgver=2.7
pkgrel=4
pkgdesc="ISO AAC audio decoder (32 bit)"
arch=('x86_64')
url="http://www.audiocoding.com/"
license=('GPL' 'custom:FAAD2')
depends=('lib32-glibc' ${_pkgbase})
makedepends=('gcc-multilib')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/sourceforge/faac/${_pkgbase}-${pkgver}.tar.bz2"
	'LICENSE')
md5sums=('4c332fa23febc0e4648064685a3d4332'
         'ec95f38142e14fb46c849503897a37a0')
sha1sums=('b0e80481d27ae9faf9e46c8c8dfb617a0adb91b5'
          '00edd9b30d8d5098b5e91c03c595d96a3bbe56fd')

build() {
    export CC='gcc -m32'
    export CXX='g++ -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
    
    cd "${srcdir}/${_pkgbase}-${pkgver}"    
    ./configure --prefix=/usr --libdir=/usr/lib32
    make CFLAGS="${CFLAGS} -fPIC"
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    
    make DESTDIR="${pkgdir}" install
    rm -rf "${pkgdir}/usr"/{bin,include,share}
    install -Dm644 "${srcdir}/LICENSE" \
    		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
