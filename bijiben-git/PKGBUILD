# Maintainer: pictuga <generaleduweb@gmail.com>

_name=bijiben
pkgname=${_name}-git
pkgver=3.9.3.25.g917756b
pkgrel=1
pkgdesc="Write out notes, every detail matters"
arch=(i686 x86_64)
url="https://live.gnome.org/Bijiben"
license=(GPL)
depends=(libtracker-sparql clutter-gtk libzeitgeist webkitgtk3 hicolor-icon-theme desktop-file-utils)
makedepends=(intltool itstool git gnome-common yelp-tools)
options=(!emptydirs)
provides=($_name)
conflicts=($_name)
install=$pkgname.install
source=(git://git.gnome.org/$_name
        git://git.gnome.org/libgd)
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd $_name
  git describe | sed 's/^v//;s/-/./g;s/_/./g' | cut -d'.' -f2-
}

prepare() {
  cd $_name
  git submodule init
  git config submodule.libgd.url "$srcdir/libgd"
  git submodule update
}

build() {
  cd $_name
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$_name
  make
}

package() {
  cd $_name
  make DESTDIR="$pkgdir" install
}
