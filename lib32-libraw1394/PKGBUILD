# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libraw1394
pkgname=lib32-${_pkgbase}
pkgver=2.0.7
pkgrel=1
pkgdesc="Provides an API to the Linux IEEE1394 (FireWire) driver (32 bit)"
arch=('x86_64')
url="https://ieee1394.wiki.kernel.org/index.php/Main_Page"
license=('LGPL2.1')
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/libs/ieee1394/${_pkgbase}-${pkgver}.tar.bz2)
md5sums=('997ef18ab8e2e21f3966dff00e92be28')

build() {
   export CC='gcc -m32'
   export CXX='g++ -m32'
   export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
   cd "${srcdir}/${_pkgbase}-${pkgver}"
   ./configure --prefix=/usr --libdir=/usr/lib32
   make
}

package () {
   cd "${srcdir}/${_pkgbase}-${pkgver}"
   make DESTDIR="${pkgdir}" install
   rm -rf "${pkgdir}"/usr/{bin,include,share}
}

