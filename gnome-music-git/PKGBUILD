# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_name=gnome-music
pkgname=$_name-git
pkgver=411.d55c12a
pkgrel=1
pkgdesc="Play and organize your music collection"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('grilo-plugins' 'gstreamer' 'libgee' 'gjs' 'tracker'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gobject-introspection' 'gnome-common' 'git')
provides=("$_name")
conflicts=("$_name")
install=$_name-git.install
options=(!libtool)
source=(git://git.gnome.org/$_name
        git://git.gnome.org/libgd)
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd $_name
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd $_name
  git submodule init
  git config submodule.libgd.url "$srcdir/libgd"
  git submodule update
}

build() {
  cd $_name
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd $_name
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
