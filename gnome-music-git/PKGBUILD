# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=gnome-music-git
pkgver=171.2656c65
pkgrel=1
pkgdesc="Play and organize your music collection"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('grilo-plugins' 'gstreamer0.10' 'libgee' 'desktop-file-utils')
makedepends=('gjs' 'intltool' 'gobject-introspection' 'gnome-common' 'git')
provides=('gnome-music')
conflicts=('gnome-music')
install=gnome-music-git.install
options=(!libtool)
source=('git://git.gnome.org/gnome-music')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir"/gnome-music
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir"/gnome-music
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir"/gnome-music
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
