# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=lib32-soundtouch
pkgver=1.6.0
pkgrel=1
pkgdesc="An audio processing library (32 bit)"
arch=('x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('lib32-gcc-libs' 'soundtouch')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://www.surina.net/soundtouch/soundtouch-${pkgver}.tar.gz")
md5sums=('aa1c63d4d67b033f044a6a48d2be5bdd')

build() {
  cd "${srcdir}/soundtouch"
  ./bootstrap
  ./configure --prefix=/usr --libdir=/usr/lib32 \
  	      --enable-shared --with-pic CXX='g++ -m32'
  make
}

check() {
  cd "${srcdir}/soundtouch"
  make check
}

package() {
  cd "${srcdir}/soundtouch"
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,doc,include,share}
}

