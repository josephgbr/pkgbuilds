# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Judd <jvinet@zeroflux.org>

_pkgbase=libevent
pkgname=lib32-${_pkgbase}
pkgver=2.0.21
pkgrel=1
pkgdesc="An event notification library (32 bit)"
arch=('x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
depends=('lib32-openssl' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=(https://github.com/downloads/${_pkgbase}/${_pkgbase}/${_pkgbase}-$pkgver-stable.tar.gz)
md5sums=('b2405cc9ebf264aa47ff615d9de527a2')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKGC_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd ${_pkgbase}-${pkgver}-stable
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  make -C ${_pkgbase}-${pkgver}-stable DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include}
}
