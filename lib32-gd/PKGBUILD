# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gd
pkgname=lib32-${_pkgbase}
pkgver=2.0.36RC1
pkgrel=4
pkgdesc="Library for the dynamic creation of images by programmers (32 bit)"
arch=('x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('lib32-libpng' 'lib32-fontconfig' 'lib32-libjpeg' "${_pkgbase}")
makedepends=('gcc-multilib') #perl
options=('!libtool')
source=("ftp://ftp.archlinux.org/other/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2"
        'libpng14.patch')
md5sums=('e876979ca3130623a4e7866d9579171a'
         '7921556f4960e8d66cf4fb67ea7101b5')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'  
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/libpng14.patch"
  ./configure --prefix=/usr --without-xpm --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  rm -rf "$pkgdir/usr"/{bin,include}
}
