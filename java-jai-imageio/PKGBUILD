# Maintainer: josepghbr <rafael.f.f1@gmail.com>

pkgname=java-jai-imageio
pkgver=1.1
pkgrel=1
pkgdesc="Pluggable architecture for working with images"
arch=(i686 x86_64)
  [ "${CARCH}" = 'i686' ]   && _arch='i586'
  [ "${CARCH}" = 'x86_64' ] && _arch='amd64'
url="http://java.sun.com/products/java-media/jai/iio.html"
license=('custom')
depends=('java-environment' 'glibc')
install=java-jai-imageio.install

_jaiimageio="jai_imageio-${pkgver/./_}"
source=(http://download.java.net/media/jai-imageio/builds/release/$pkgver/${_jaiimageio}-lib-linux-${_arch}.tar.gz
	jai-imageio.profile.csh
	jai-imageio.profile)

[ "${CARCH}" = 'i686' ] && md5sums=('8f467118068a7dfefbe1c22ab43a22f3')
[ "${CARCH}" = 'x86_64' ] && md5sums=('163a33949d92930372dd1f59702f791e')
  md5sums=("${md5sums[@]}"
  	   '3e634089cb081153a46306ff75eb22de'
           '97b99bff1e96f56462fc886a689a2dc1')

package() {
  cd "$srcdir"
  
  # profiles
  install -D ${srcdir}/jai-imageio.profile \
    ${pkgdir}/etc/profile.d/jai-imageio.sh
  install -D ${srcdir}/jai-imageio.profile.csh \
    ${pkgdir}/etc/profile.d/jai-imageio.csh
    
  # main files
  install -d ${pkgdir}/opt
  cp -a $_jaiimageio ${pkgdir}/opt/jai-imageio
  cd ${pkgdir}/opt/jai-imageio
  rm UNINSTALL-jai_imageio
  
  # licenses
  install -d $pkgdir/usr/share/licenses/$pkgname
  for file in COPYRIGHT \
  	   ENTITLEMENT \
  	   LICENSE \
  	   THIRDPARTYLICENSEREADME \
  	   DISTRIBUTIONREADME
  	   do     
     install -m644 $file-jai_imageio.txt $pkgdir/usr/share/licenses/$pkgname
  done
}
