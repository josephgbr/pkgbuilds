# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Josef Lusticky <evramp@gmail.com>

pkgname=counter-strike-2d
pkgver=0.1.2.2
_ver=0122
pkgrel=1
pkgdesc="A more than just a freeware clone of the well known game Counter-Strike"
arch=('i686' 'x86_64')
url="http://www.cs2d.com/"
license=('custom')
changelog=ChangeLog
if [ "$CARCH" == "i686" ]; then
  depends=('freetype2' 'libtxc_dxtn' 'libxcursor' 'mesa' 'openal' 'glu' 'lua')
elif [ "$CARCH" == "x86_64" ]; then
  depends=('lib32-freetype2' 'lib32-libtxc_dxtn' 'lib32-libxcursor'
           'lib32-mesa' 'lib32-openal' 'lib32-glu' 'lua')
fi
makedepends=('unzip' 'curl')

backup=(usr/share/cs2d/sys/autobuy.cfg
        usr/share/cs2d/sys/autoexec.cfg
        usr/share/cs2d/sys/config.cfg
        usr/share/cs2d/sys/controls.cfg
        usr/share/cs2d/sys/editor.cfg
        usr/share/cs2d/sys/filters.cfg
        usr/share/cs2d/sys/mapcycle.cfg
        usr/share/cs2d/sys/more.cfg
        usr/share/cs2d/sys/server.cfg
        usr/share/cs2d/sys/usgn.dat
        usr/share/cs2d/sys/usgn_pw.dat)

#https://bbs.archlinux.org/viewtopic.php?id=141195
_url=http://www.unrealsoftware.de/get.php?get
grabcid() {
  local file=cs2d_${_ver}_linux.zip
  echo "$(curl -Ss "$_url=${file}&p=1" | 
            grep -o '"get.php[^"]\+"' | cut -d'"' -f2 | sed 's/&amp;/\&/g')"
}
_cid=$(grabcid) # this will hide the cmd line above from AUR interface

source=(cs2d_${_ver}_linux.zip::"$_url=cs2d_${_ver}_linux.zip&p=1&cid=${_cid}"
        cs2d_${_ver}_win.zip::"$_url=cs2d_${_ver}_win.zip&p=1&cid=${_cid}"
        cs2d.desktop
        cs2d.png)
noextract=(cs2d_${_ver}_linux.zip
           cs2d_${_ver}_win.zip)
md5sums=('9f1d80e39e23b8f5729f2c70cc4b931d'
         '5218934de95a81cecc9e6e2760f813c0'
         '9bd65c757a3d3613c152fa21cad22355'
         'b4bb759de5f6db90a0363f37c4d400c8')
sha1sums=('a026a7f21c5903565a5bc7c72f82308ebddf55df'
          'e985c8014ebac3b347657fde3cfae16f91366936'
          '04f5bb933d95d6085a08dc0f1937f583f9a2e550'
          '313200eb8d8a120f6abed5c052798def9ea6269e')

package() {
  install -d -m 755 "$pkgdir"/usr/share/cs2d
  unzip cs2d_${_ver}_win.zip -d "$pkgdir"/usr/share/cs2d
  unzip cs2d_${_ver}_linux.zip -d "$pkgdir"/usr/share/cs2d
  
  # Must have write permission to all users to provide better gaming experience
  for file in server.cfg filters.cfg autobuy.cfg mapcycle.cfg more.cfg \
              autoexec.cfg config.cfg editor.cfg controls.cfg usgn_pw.dat \
              usgn.dat ; do
    chmod 666 "$pkgdir"/usr/share/cs2d/sys/$file
  done
  chmod 777 "$pkgdir"/usr/share/cs2d/sys/logs
  chmod 777 "$pkgdir"/usr/share/cs2d/sys/core
  
  install -d -m 755 "$pkgdir"/usr/bin
  ln -s /usr/share/cs2d/CounterStrike2D "$pkgdir"/usr/bin/cs2d
  chmod +x "$pkgdir"/usr/share/cs2d/CounterStrike2D
  
  install -Dm644 cs2d.png "$pkgdir"/usr/share/pixmaps/cs2d.png
  install -Dm755 cs2d.desktop "$pkgdir"/usr/share/applications/cs2d.desktop
  install -dm755 "$pkgdir"/usr/share/licenses/cs2d/
  sed -n 62,84p "$pkgdir"/usr/share/cs2d/Readme.txt > \
                "$pkgdir"/usr/share/licenses/cs2d/license
  
  # Fix mess charset in file names with accents
  cd "$pkgdir"/usr/share/cs2d/sys/language/
  mv Brazilian*.txt "Portuguese (Brasil)".txt
  mv Bulgarian*.txt Bulgarian.txt
  mv Czech*.txt Czech.txt
  mv Spanish*.txt Spanish.txt
  mv Turkish*.txt Turkish.txt
}
