# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-gtk3
pkgver=3.6.2
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gtk.org/"
depends=('lib32-atk' 'lib32-gdk-pixbuf2' 'lib32-libxcomposite'
	 'lib32-libcups' 'lib32-libxcursor' 'lib32-libxdamage'
	 'lib32-libxi' 'lib32-libxinerama' 'lib32-libxrandr' 'gtk3'
	 'lib32-pango' 'lib32-cairo' 'lib32-colord' 'lib32-at-spi2-atk')
makedepends=('gcc-multilib' 'gobject-introspection')
options=('!libtool' '!docs')
install=gtk3.install
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/3.6/gtk+-${pkgver}.tar.xz"
        'wacom.patch')
sha256sums=('aaccf2e1576338acf37816290578e8f19888935f381665b3847076be25fe513c'
            '86bda95a14a99d0f596c4ecb2ed715689f71c207c65dfc90a39d4ae7f1c0c0f5')

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd gtk+-${pkgver}
  
  # Partially revert BGO#673440 in order to fix BGO#674157
  patch -Np1 -i ../wacom.patch
  
  CXX=/bin/false ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-gtk2-dependency \
      --disable-schemas-compile \
      --libdir=/usr/lib32
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool  
  make
}

package() {
  make -C gtk+-${pkgver} DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  rm "${pkgdir}"/usr/bin/{gtk-launch,gtk3-{demo,demo-application,widget-factory}}
  rm -rf "${pkgdir}"/usr/{include,share} "${pkgdir}/etc"
}
