# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-gtk3
pkgver=3.6.4
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gtk.org/"
depends=('lib32-atk' 'lib32-gdk-pixbuf2' 'lib32-libxcomposite'
	 'lib32-libcups' 'lib32-libxcursor' 'lib32-libxdamage'
	 'lib32-libxi' 'lib32-libxinerama' 'lib32-libxrandr' 'gtk3'
	 'lib32-pango' 'lib32-cairo' 'lib32-colord' 'lib32-at-spi2-atk'
	 'lib32-wayland' 'lib32-libxkbcommon')
makedepends=('gcc-multilib' 'gobject-introspection' 'lib32-mesa')
options=('!libtool' '!docs')
install=gtk3.install
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/3.6/gtk+-${pkgver}.tar.xz")
sha256sums=('dbda37986f934f986c0bb20f2b0c59f2717a2e5c098370ad69677ff89295f1a1')

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd gtk+-${pkgver}
  
  CXX=/bin/false ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-gtk2-dependency \
      --disable-schemas-compile \
      --enable-x11-backend \
      --enable-broadway-backend \
      --enable-wayland-backend \
      --libdir=/usr/lib32
      
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool  
  make
}

package() {
  make -C gtk+-${pkgver} DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  rm "${pkgdir}"/usr/bin/{gtk-launch,gtk3-{demo,demo-application,widget-factory}}
  rm -rf "${pkgdir}"/usr/{include,share} "${pkgdir}/etc"
}
