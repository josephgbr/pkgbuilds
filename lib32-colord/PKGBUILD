# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=colord
pkgname=lib32-${_pkgbase}
pkgver=1.0.0
pkgrel=1
pkgdesc="System daemon for managing color devices (32 bit)"
arch=('x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lib32-libgusb' 'lib32-sqlite' 'lib32-polkit' 'lib32-lcms2' 'lib32-glib2' 'lib32-systemd' "${_pkgbase}")
makedepends=('intltool' 'gobject-introspection' 'vala') #'docbook2x'
options=('!libtool')
source=(${url}/releases/${_pkgbase}-${pkgver}.tar.xz)
sha1sums=('a01547080f0905dacca37c5ded3886431144a36a')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd $_pkgbase-$pkgver
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --with-systemdsystemunitdir=/usr/lib/systemd/system --disable-static \
    --libexecdir=/usr/lib32/colord --libdir=/usr/lib32 --disable-manpages \
    --disable-nls --enable-vala --disable-bash-completion #--enable-sane
  make
}

package() {
  make -C $_pkgbase-$pkgver DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,lib,var,usr/{bin,include,lib,share}}
}
