# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Martin Stolpe <martinstolpe [at] gmail dot com>

_pkgbase=libjpeg6-turbo
pkgname=lib32-${_pkgbase}
pkgver=1.2.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('BSD')
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('nasm' 'gcc-multilib')
provides=('lib32-libjpeg6')
conflicts=('lib32-libjpeg6')
options=('!libtool')
source=(http://sourceforge.net/projects/libjpeg-turbo/files/${pkgver}/libjpeg-turbo-${pkgver}.tar.gz)
md5sums=('f61e60ff01381ece4d2fe65eeb52669e')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd libjpeg-turbo-${pkgver}
  sed -i "s|NAFLAGS='-felf64 -DELF -D__x86_64__'|NAFLAGS='-felf32 -DELF -D__x86_64__'|" configure
  ./configure --prefix=/usr --mandir=/usr/share/man --libdir=/usr/lib32 --without-simd
  make
}

package() {
  cd libjpeg-turbo-${pkgver}

  make DESTDIR="${pkgdir}/" install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
  rm ${pkgdir}/usr/lib32/lib{turbo,}jpeg.{a,so}
  
  mkdir -p "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/${pkgname}
}
