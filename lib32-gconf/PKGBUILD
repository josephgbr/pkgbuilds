# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gconf
pkgname=lib32-${_pkgbase}
pkgver=3.2.5
pkgrel=2
pkgdesc="A configuration database system (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gnome.org"
depends=('lib32-libxml2' 'lib32-polkit' 'lib32-libldap' 'lib32-dbus-glib' 'lib32-gtk3' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib' 'gobject-introspection') #gtk-doc
options=('!libtool')
install=${_pkgbase}.install
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/3.2/GConf-${pkgver}.tar.xz
        gconf-dbus-fix-use-after-free.patch
        gconf-dbus-fix-shutdown.patch
        gsettings-schema-convert-dont-fail.patch
        gconf-reload.patch
        01_xml-gettext-domain.patch)
sha256sums=('4ddea9503a212ee126c5b46a0a958fd5484574c3cb6ef2baf38db02e819e58c6'
            '4687608e7ddbee083873c3482c9ce7661be90d78835a1b653edefb4ff60d87ee'
            '973dc08e2957ca8179641917faf02faedb29ad39fd41bf26c765a4ed1e29fae8'
            'be6f084a31229e8edfd6936005c6bb4f2d1548b777df5937923b4702f7a9ac19'
            '567b78d8b4b4bbcb77c5f134d57bc503c34867fcc6341c0b01716bcaa4a21694'
            'c883dec2b96978874a53700cfe7f26f24f8296767203e970bc6402b4b9945eb8')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd GConf-${pkgver}
  # Upstream fixes from git
  patch -Np1 -i "${srcdir}"/gconf-dbus-fix-shutdown.patch
  patch -Np1 -i "${srcdir}"/gsettings-schema-convert-dont-fail.patch
#  patch -Np1 -i "${srcdir}"/gconf-dbus-fix-use-after-free.patch
  
  # Patch from fedora - reloads gconf after installing schemas
  patch -Np1 -i "${srcdir}"/gconf-reload.patch
  # http://bugzilla.gnome.org/show_bug.cgi?id=568845
  patch -Np1 -i "${srcdir}"/01_xml-gettext-domain.patch

  # Python2 fix
  sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert

  ./configure \
  	--prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-defaults-service --disable-static --disable-orbit \
	--with-gtk=3.0 --libdir=/usr/lib32 --libexecdir=/usr/lib32/GConf \
	--disable-gtk-doc

  make pkglibdir=/usr/lib32/GConf
}

package() {
  make -C GConf-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

