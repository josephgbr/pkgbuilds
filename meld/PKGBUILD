# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_name=meld
pkgname=$_name-git
pkgver=1.7.3.62.gd37fb63
pkgrel=1
pkgdesc='Visual diff and merge tool'
arch=('any')
url='http://meldmerge.org/'
license=('GPL')
depends=('pygtk' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils' 'git' 'gnome-common')
optdepends=('pygtksourceview2: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
provides=($_name)
conflicts=($_name)
install=meld.install
options=('!emptydirs')
source=(git://git.gnome.org/$_name)
sha256sums=('SKIP')

pkgver() {
  cd $_name
  git describe --tags | sed s/-/./g
}

prepare() {
  cd $_name
  sed -i 's/update-/#update-/' Makefile
  sed -i 's/env python/env python2/' bin/meld
}

build() {
  cd $_name
  make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
  cd $_name
  make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="$pkgdir" install
}
