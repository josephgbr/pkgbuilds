# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libdc1394
pkgname=lib32-${_pkgbase}
pkgver=2.1.3
pkgrel=1
pkgdesc="High level programming interface to control IEEE 1394 based cameras (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://sourceforge.net/projects/libdc1394/"
depends=('lib32-libraw1394' 'lib32-libusb' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        'rename-usb_init.patch'
        'videodev-compile-fix.patch')
md5sums=('d8b2cbfae1b329fdeaa638da80427334'
         'bc7a4146b4d2d688305d0d42c4111941'
         '9dd11c96241b14073c3a7dd7e270fb7a')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  # FS#25652
  patch -p2 -i "${srcdir}"/rename-usb_init.patch
  # Fix build when linux/video.h exists
  patch -p2 -i "${srcdir}"/videodev-compile-fix.patch
  
  autoreconf -i
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
