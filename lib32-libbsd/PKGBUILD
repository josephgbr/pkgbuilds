# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libbsd
pkgname=lib32-$_pkgbase
pkgver=0.5.2
pkgrel=1
pkgdesc="Provides useful functions commonly found on BSD systems like strlcpy() (32 bit)"
arch=('x86_64')
url="http://libbsd.freedesktop.org"
license=('custom')
depends=('lib32-glibc' "$_pkgbase")
options=(!libtool)
source=($url/releases/$_pkgbase-$pkgver.tar.xz
        spt.patch)
md5sums=('be8b2e0dc4614699834c49693574fd1a'
         '56236fb72c8ec6cbdbd0daa5f404bccb')

prepare() {
  cd $_pkgbase-$pkgver
  
  # Paper over a firefox crash
  # http://lists.freedesktop.org/archives/libbsd/2013-June/000085.html
  patch -Np1 -i ../spt.patch
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  
  cd $_pkgbase-$pkgver
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share/man}
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
