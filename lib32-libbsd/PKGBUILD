# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libbsd
pkgname=lib32-$_pkgbase
pkgver=0.4.2
pkgrel=1
pkgdesc="Provides useful functions commonly found on BSD systems like strlcpy() (32 bit)"
arch=('x86_64')
url="http://libbsd.freedesktop.org"
license=('custom')
depends=('lib32-glibc' "$_pkgbase")
options=(!libtool)
source=(http://libbsd.freedesktop.org/releases/$_pkgbase-$pkgver.tar.gz)
md5sums=('591fc9de4ca22f78cf87a94e648a92f4')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  cd $_pkgbase-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share/man}
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
