# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libxkbcommon
pkgname=lib32-$_pkgbase
pkgver=0.2.0
pkgrel=3
pkgdesc="Keyboard handling library using XKB data (32 bit)"
arch=(x86_64)
url="http://xkbcommon.org/"
license=('custom')
depends=(lib32-glibc $_pkgbase)
makedepends=(doxygen xorg-util-macros gcc-multilib)
source=("http://xkbcommon.org/download/$_pkgbase-$pkgver.tar.bz2")
md5sums=('2be3d4a255d02c7d46fc6a9486f21f6a')
options=('!libtool')

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd $srcdir/$_pkgbase-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share}
  
  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s $_pkgbase "$pkgdir"/usr/share/licenses/$pkgname
}
