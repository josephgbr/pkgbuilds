# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libxkbcommon
pkgname=lib32-$_pkgbase
pkgver=0.3.0
pkgrel=1
pkgdesc="Keyboard handling library using XKB data (32 bit)"
arch=(x86_64)
url="http://xkbcommon.org/"
license=(custom)
depends=(lib32-glibc $_pkgbase)
makedepends=(doxygen xorg-util-macros gcc-multilib)
options=('!libtool')
source=("http://xkbcommon.org/download/$_pkgbase-$pkgver.tar.xz")
md5sums=('22a046100738f99b4cc0297aa210f4e4')

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd $_pkgbase-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share}
  
  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s $_pkgbase "$pkgdir"/usr/share/licenses/$pkgname
}
