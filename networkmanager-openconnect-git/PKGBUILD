# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=networkmanager-openconnect-git
pkgver=20130101
pkgrel=1
pkgdesc="NetworkManager VPN integration for openconnect"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/NetworkManager/"
license=('GPL')
depends=("networkmanager" 'openconnect' 'gconf' 'libgnome-keyring')
makedepends=('intltool' 'git')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
provides=('networkmanager-openconnect')
conflicts=('networkmanager-openconnect')
options=('!libtool')
install=networkmanager-openconnect-git.install

_gitroot="git://git.gnome.org/network-manager-openconnect"
_gitname="network-manager-openconnect"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
	--disable-static
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR=$pkgdir install
}
