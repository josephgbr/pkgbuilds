# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=raptor
pkgname=lib32-${_pkgbase}
pkgver=2.0.9
pkgrel=1
pkgdesc="A C library that parses RDF/XML/N-Triples into RDF triples (32 bit)"
arch=('x86_64')
url="http://librdf.org/raptor"
depends=('lib32-curl' 'lib32-libxslt' 'lib32-icu' "${_pkgbase}")
makedepends=('gcc-multilib')
license=('LGPL')
options=('!libtool')
source=("http://librdf.org/dist/source/raptor2-${pkgver}.tar.gz")
md5sums=('4ceb9316488b0ea01acf011023cf7fff')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd raptor2-${pkgver}
  ./configure --prefix=/usr \
    --disable-static \
    --with-yajl=no \
    --with-icu-config=/usr/bin/icu-config-32 \
    --libdir=/usr/lib32
  make
}

package() {
  cd raptor2-${pkgver}
  make prefix="${pkgdir}"/usr libdir="${pkgdir}"/usr/lib32 install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
