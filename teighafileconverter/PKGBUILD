# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teighafileconverter
pkgver=3.8.1
_ver=3.08.1
pkgrel=2
pkgdesc="Free cross-platform application for the conversion of .dwg and .dxf files to/from different versions"
arch=('i686' 'x86_64')
url="http://www.opendesign.com/guestfiles/TeighaFileConverter"
license=('custom')

if [ $CARCH == i686 ]; then
  depends=('qt4' 'bash' 'hicolor-icon-theme')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-qt4' 'bash' 'hicolor-icon-theme')
fi

install=${pkgname}.install
source=(http://www.opendesign.com/files/guestdownloads/TeighaFileConverter/TeighaFileConverter_${pkgver}_i386.deb)
md5sums=('35f6d46cbf1db097f033cf2022d0d8a7')

build() {
  [ ! -d ${pkgname}-${pkgver} ] && mkdir ${pkgname}-${pkgver}
  
  ar xo TeighaFileConverter_${pkgver}_i386.deb data.tar.gz
  tar xf data.tar.gz -C ${pkgname}-${pkgver}
}

package() {
  cd ${pkgname}-${pkgver}
  
  # persistent application storage (libs, executables, etc.)
  if [ $CARCH == i686 ]; then
    appstoragedir=usr/lib/teighafileconverter
  elif [ $CARCH == x86_64 ]; then
    appstoragedir=usr/lib32/teighafileconverter
  fi
  for file in $(ls usr/bin/TeighaFileConverter_${_ver}/); do
    install -Dm755 usr/bin/TeighaFileConverter_${_ver}/${file} \
        "${pkgdir}/${appstoragedir}/${file}"
  done
  
  # binary
  install -Dm755 usr/bin/TeighaFileConverter \
    "${pkgdir}/usr/bin/teighafileconverter"
  sed -e "s#usr/bin/TeighaFileConverter_${_ver}#${appstoragedir}#" \
    -i "${pkgdir}/usr/bin/${pkgname}"
  
  # desktop item
  install -Dm644 usr/share/applications/TeighaFileConverter_${_ver}.desktop \
    "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  sed -e "s#/TeighaFileConverter#/teighafileconverter#" \
    -i "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  
  # hicolor icons
  for icon in $(find usr/share/icons -type f); do
    install -Dm644 ${icon} \
        "${pkgdir}/${icon}"
  done
  
  # copyright
  install -Dm644 usr/share/doc/TeighaFileConverter/copyright \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
  
}
