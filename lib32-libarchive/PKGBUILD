# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libarchive
pkgname=lib32-${_pkgbase}
pkgver=3.0.4
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats (32 bit)"
arch=('x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('lib32-bzip2' 'lib32-lzma' 'lib32-acl' 'lib32-openssl' 'lib32-expat' "${_pkgbase}")
makedepends=('gcc-multilib' 'lib32-zlib')
source=("https://github.com/downloads/libarchive/libarchive/libarchive-${pkgver}.tar.gz"
        'libarchive-3.0.x-fix-mtree-writer.patch')
md5sums=('af443ca9a10ddbcbf00f7ae34ca7fc16'
         '6d36a50a7282db6576bd1fbc23f08055')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  patch -Np1 -i "${srcdir}"/libarchive-3.0.x-fix-mtree-writer.patch
  ./configure --prefix=/usr --without-xml2 --libdir=/usr/lib32 --libexecdir=/usr/lib32/${_pkgbase}
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm755 "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/${pkgname}
}
